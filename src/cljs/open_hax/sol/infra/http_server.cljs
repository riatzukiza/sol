(ns open-hax.sol.infra.http-server
  "Fastify HTTP server construction and lifecycle helpers."
  (:require ["fastify" :default Fastify]
            ["@fastify/cors" :default fastifyCors]
            ["@fastify/websocket" :default fastifyWebsocket]
            ["@fastify/formbody" :default fastifyFormbody]))

(defn create-app!
  []
  (Fastify #js {:logger true
                :bodyLimit (* 50 1024 1024)
                :requestTimeout 600000
                :connectionTimeout 600000
                :forceCloseConnections true}))

(defn ensure-json-empty-body-parser!
  "Allow Content-Type: application/json with empty bodies."
  [^js app]
  (.addContentTypeParser app
                         "application/json"
                         #js {:parseAs "string"}
                         (fn [_req body done]
                           (try
                             (done nil (if (= body "") #js {} (js/JSON.parse body)))
                             (catch :default err
                               (done err))))))

(defn add-hook!
  [^js app hook-name handler]
  (.addHook app hook-name handler))

(defn ^:async register-default-plugins!
  [^js app]
  (await (.register app fastifyCors #js {:origin true}))
  (await (.register app fastifyFormbody))
  (await (.register app fastifyWebsocket)))

(defn listen!
  [^js app host port]
  (.listen app #js {:host host :port port}))

(defn close!
  [^js app]
  (try
    (let [result (.close app)]
      (if (some? result)
        result
        (js/Promise.resolve true)))
    (catch :default err
      (js/Promise.reject err))))
